/* 数独 */

// 行内各列(r1.c1, r1.c2, r1.c3, ...)の値はすべて異なる (左のdisj)
// 各行同列(r1.c1, r2.c1, r3.c1, ...)の値もすべて異なる (右のdisj)
abstract sig Row {disj c1,c2,c3,c4,c5,c6,c7,c8,c9: disj (1+2+3+4+5+6+7+8+9)}
one sig r1,r2,r3,r4,r5,r6,r7,r8,r9 extends Row {}

fact {
    // 各 3x3 領域の数字はそれぞれ９種類
    #(r1+r2+r3).(c1+c2+c3) = 9
    #(r1+r2+r3).(c4+c5+c6) = 9
    #(r1+r2+r3).(c7+c8+c9) = 9
    #(r4+r5+r6).(c1+c2+c3) = 9
    #(r4+r5+r6).(c4+c5+c6) = 9
    #(r4+r5+r6).(c7+c8+c9) = 9
    #(r7+r8+r9).(c1+c2+c3) = 9
    #(r7+r8+r9).(c4+c5+c6) = 9
    #(r7+r8+r9).(c7+c8+c9) = 9
}

例: run {
    r1.c1=4 && r1.c4=1 && r1.c9=8
    r2.c2=7 && r2.c3=8 && r2.c5=6 && r2.c7=4 && r2.c8=2
    r3.c9=3
    r4.c6=7
    r5.c2=3 && r5.c5=2 && r5.c6=1
    r6.c5=5
    r7.c1=2 && r7.c2=5 && r7.c4=4 && r7.c8=3 && r7.c9=1
    r8.c3=6 && r8.c5=3 && r8.c8=8
    r9.c6=6 && r9.c7=5 && r9.c8=7
} for 5 int

ノーヒント: run {} for 5 int
